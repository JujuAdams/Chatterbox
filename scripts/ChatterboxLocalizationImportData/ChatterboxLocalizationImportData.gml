// Feather disable all

/// Takes a struct/array data structure generated by `ChatterboxLocalizationExportData()` and
/// applies it. This function is provided as an alternative to CSV-based localization.

function ChatterboxLocalizationImportData(_data)
{
    static _system = __ChatterboxSystem();
    
    var _map = _system.__localisationMap;
    ds_map_clear(_map);
    
    var _f = 0;
    repeat(array_length(_data))
    {
        var _file_struct = _data[_f];
        var _filename    = _file_struct.filename;
        var _node_array  = _file_struct.nodes;
        
        var _n = 0;
        repeat(array_length(_node_array))
        {
            var _node_struct  = _node_array[_n];
            var _node_title   = _node_struct.title;
            var _string_array = _node_struct.strings;
            
            var _prefix = _filename + ":" + _node_title + ":";
            
            var _s = 0;
            repeat(array_length(_string_array))
            {
                var _string_struct = _string_array[_s];
                
                _map[? _prefix + _string_struct.line_id] = _string_struct.content;
                
                ++_s;
            }
            
            ++_n;
        }
        
        ++_f;
    }
}
