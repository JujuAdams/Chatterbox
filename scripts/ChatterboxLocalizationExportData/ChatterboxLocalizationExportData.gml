// Feather disable all

/// Parses an array of ChatternScript files stored in your project's Included Filess directory and
/// creates a struct/array data structure that contains all strings in those source files. The
/// ChatterScript files are modified by this function such that they link up to the data structure.
/// 
///   !!! WARNING !!!
///   THIS FUNCTION WILL MODIFY SOURCE FILES ON DISK INSIDE YOUR PROJECT
///   ENSURE YOU HAVE BACKED UP YOUR WORK IN SOURCE CONTROL
/// 
/// You can then save out the data (perhaps using `json_stringify()`) to a file. Translators can
/// modify the file and the localizaed strings should then be loaded by the sister function
/// `ChatterboxLocalizationImportData()`.
/// 
/// The data structure generated by this function follows the following format:
/// 
/// [
///     {
///         filename: <string>,
///         nodes: [
///             {
///                 title: <string>,
///                 strings: [
///                     {
///                         hash: <string>,
///                         content: <string>,
///                     },
///                     ...
///                 ]
///             },
///             ...
///         ]
///    ],
///    ...
/// ]
/// 
/// @param chatterPathArray   Array of paths to source ChatterScript files, relative to CHATTERBOX_INCLUDED_FILES_SUBDIRECTORY

function ChatterboxLocalizationExportData(_chatter_path_array)
{
    static _system = __ChatterboxSystem();
    
    var _json = [];
    
    var _root_directory = __ChatterboxLocGetRootDirectory();
    
    if (!is_array(_chatter_path_array)) _chatter_path_array = [_chatter_path_array];
    
    var _count = array_length(_chatter_path_array);
    var _i = 0;
    repeat(_count)
    {
        var _local_path    = __ChatterboxReplaceBackslashes(_chatter_path_array[_i]);
        var _absolute_path = __ChatterboxReplaceBackslashes(_root_directory + _local_path);
        
        var _buffer = buffer_load(_absolute_path);
        var _source = new __ChatterboxClassSource(_local_path, _buffer, false);
        
        var _buffer_batch = new __ChatterboxBufferBatch();
        _buffer_batch.__FromBuffer(_buffer);
        
        _source.__BuildLocalisation(_json, _buffer_batch);
        
        //Save out the modified ChatterScript file
        var _buffer = _buffer_batch.__GetBuffer();
        
        var _size = buffer_get_size(_buffer);
        
        //We artificially add a null when parsing the source buffer. Let's trim off the final null when re-saving it
        if (buffer_peek(_buffer, buffer_get_size(_buffer)-1, buffer_u8) == 0x00)
        {
            --_size;
        }
        
        buffer_save_ext(_buffer, _absolute_path, 0, _size);
        _buffer_batch.__Destroy();
        
        ++_i;
    }
    
    return _json;
}
